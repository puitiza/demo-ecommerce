plugins {
    id 'org.springframework.boot' version '3.5.5' apply false
    id 'io.spring.dependency-management' version '1.1.7' apply false
    id 'org.sonarqube' version "6.3.1.5724"
}

allprojects {
    group = 'com.ecommerce'
    version = '0.0.1-SNAPSHOT'

    repositories {
        mavenCentral()
    }
}

ext {
    set('springCloudVersion', '2025.0.0')
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }

    tasks.named('test') {
        useJUnitPlatform()
    }

    afterEvaluate {//Skip infrastructure modules for Sonarqube
        if (name in ['server-gateway', 'server-discovery', 'server-config']) {
            sonarqube {
                skipProject = true
            }
            tasks.named('test') {
                enabled = false
            }
        }
    }
}

// Define reusable dependency sets as methods
ext {
    applyTracingDependencies = { dependencyHandler ->
        dependencyHandler.implementation 'org.springframework.boot:spring-boot-starter-actuator'
        dependencyHandler.implementation 'io.micrometer:micrometer-tracing-bridge-otel'
        dependencyHandler.implementation 'io.opentelemetry:opentelemetry-exporter-otlp'
        dependencyHandler.implementation 'io.micrometer:micrometer-registry-prometheus'
    }
    applySecurityDependencies = { dependencyHandler ->
        dependencyHandler.implementation 'org.springframework.boot:spring-boot-starter-security'
        dependencyHandler.implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    }
    applyDiscoveryDependencies = { dependencyHandler ->
        dependencyHandler.implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
        dependencyHandler.implementation 'org.springframework.cloud:spring-cloud-starter-config'
    }
    applyKafkaDependencies = { dependencyHandler ->
        dependencyHandler.implementation 'org.springframework.kafka:spring-kafka'
    }
    applyRestDocsDependencies = { dependencyHandler ->
        dependencyHandler.testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
        dependencyHandler.testImplementation 'org.springframework.restdocs:spring-restdocs-core'
        dependencyHandler.testImplementation 'org.asciidoctor:asciidoctorj:3.0.0'
    }
}

// SonarQube Configuration
sonarqube {
    properties {
        property "sonar.projectKey", "demo-ecommerce"
        property "sonar.projectName", "Demo E-commerce"
        property "sonar.host.url", System.getenv("SONAR_HOST_URL") ?: "http://localhost:9000"
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.modules", "order-service,product-service"
        property "sonar.coverage.exclusions", "**/*Application*.java, **/config/**"
        property "sonar.token", resolveSonarToken()
    }
}

String resolveSonarToken() {
    // 1. Environment variable (CI/CD)
    if (System.getenv("SONAR_TOKEN")) {
        return System.getenv("SONAR_TOKEN")
    }

    // 2. Local token file (Development)
    def tokenFile = file("imports/sonarqube/sonar-token.txt")
    if (!tokenFile.exists()) {
        tokenFile = file("../imports/sonarqube/sonar-token.txt")
    }

    if (tokenFile.exists()) {
        return tokenFile.text.trim()
    }

    throw new GradleException("SonarQube token not found. Run 'docker-compose up sonarqube' first")
}